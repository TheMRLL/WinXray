import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=938;bottom=638;bgcolor=16777215)
winform.add(
advancedConfig={cls="\forms\main\config\advancedConfig.aardio";left=9;top=20;right=930;bottom=594;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;edge=1;z=12};
btnUpdate={cls="plus";text="更新服务器配置";left=699;top=604;right=868;bottom=635;align="left";bgcolor=11580047;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=22}};iconText='\uF0C7';notify=1;textPadding={left=40};z=6};
btnUpdateNaiveCore={cls="plus";text="下载 / 更新 NaÏveProxy Core";left=448;top=7;right=709;bottom=35;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-17;name='FontAwesome');padding={left=18}};iconText='\uF1A0';notify=1;textPadding={left=50};z=17};
btnUpdateRules={cls="plus";text="下载 / 更新 路由规则";left=227;top=39;right=425;bottom=67;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=18}};iconText='\uF041';notify=1;textPadding={left=50};z=15};
btnUpdateSsrCore={cls="plus";text="下载 / 更新 SSR Core";left=16;top=39;right=214;bottom=67;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-17;name='FontAwesome');padding={left=18}};iconText='\uF1D8';notify=1;textPadding={left=50};z=16};
btnUpdatev2rayCore={cls="plus";text="下载 / 更新 V2ray Core";left=227;top=5;right=425;bottom=33;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=18}};iconText='\uF27D';notify=1;textPadding={left=50};z=14};
btnUpdatexrayCore={cls="plus";text="下载 / 更新 Xray Core";left=16;top=5;right=223;bottom=33;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=18}};iconText='\uF2D3';notify=1;textPadding={left=50};z=13};
editCoreConfig={cls="edit";left=9;top=72;right=930;bottom=594;db=1;dl=1;dr=1;dt=1;edge=1;hide=1;hscroll=1;multiline=1;vscroll=1;z=7};
editOutbounds={cls="edit";left=9;top=29;right=930;bottom=594;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=8};
lbTip={cls="static";text="可在下面输入多行服务器分享链接，或JSON数组( 点击字段名会显示用法说明 )：";left=11;top=9;right=899;bottom=29;color=3947580;dl=1;dr=1;dt=1;font=LOGFONT(h=-13);transparent=1;z=9};
lbTipForCoreJson={cls="static";text="tag 指定为 proxy,http_proxy 的节点属于自动维护节点 - 不可删除";left=468;top=45;right=849;bottom=68;color=4964352;dl=1;dt=1;transparent=1;z=19};
navAdvancedConfig={cls="plus";text="代理端口 / 高级配置";left=359;top=600;right=493;bottom=632;border={top=1;color=-16744448};db=1;dl=1;z=10};
navCoreConfig={cls="plus";text="Core 配置 ( JSON )";left=162;top=600;right=293;bottom=632;border={top=1;color=-16744448};db=1;dl=1;z=3};
navOutbounds={cls="plus";text="服务器配置( JSON )";left=31;top=600;right=162;bottom=632;border={top=1;color=-16744448};db=1;dl=1;z=2};
navRight={cls="plus";left=493;top=600;right=762;bottom=601;bgcolor=16777215;border={top=1;color=-16744448};db=1;dl=1;dr=1;linearGradient=180;z=4};
navSubscription={cls="plus";text="订阅";left=293;top=600;right=359;bottom=632;border={top=1;color=-16744448};db=1;dl=1;z=11};
navUwpExemption={cls="plus";text="UWP 应用配置";left=499;top=600;right=633;bottom=632;border={top=1;color=-16744448};db=1;dl=1;notify=1;z=18};
plus={cls="plus";left=9;top=600;right=31;bottom=601;border={top=1;color=-16744448};db=1;dl=1;z=5};
subscription={cls="\forms\main\config\subscription.aardio";left=9;top=19;right=930;bottom=594;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;edge=1;z=1}
)
/*}}*/

import config;
import web.json;
import win.ui.tabs;
var tbs = win.ui.tabs(winform.navOutbounds,winform.navCoreConfig,winform.navSubscription,winform.navAdvancedConfig);
tbs.margin = 0;

tbs.skin({
	foreground={
		active=0xFFFFFFFF;
		default=0x00FFFFFF;
		hover=0xFFCCCCCC;
	};
	checked={
		foreground={ 
			default=0x00FFFFFF; 
		};
		border = { 
			default = {left=1;right=1;bottom=1;color=0xFF008000} 
		}; 
	}
});

tbs.onSelchange = function(idx,strip,form){
    winform.btnUpdateSsrCore.hide = idx!=2;
	winform.btnUpdatexrayCore.hide = idx!=2;
	winform.btnUpdatev2rayCore.hide = idx!=2;
	winform.btnUpdateRules.hide = idx!=2;
	winform.btnUpdateNaiveCore.hide = idx!=2;
	winform.editCoreConfig.hide = idx!=2; 
	winform.editOutbounds.hide = idx!=1;	
	winform.subscription.hide = idx!=3;
	winform.advancedConfig.hide = idx!=4;
	winform.btnUpdate.hide = idx>2;
	winform.lbTip.hide = idx!=1; 
	winform.lbTipForCoreJson.hide = true; 
	
	if(idx==1){
		winform.btnUpdate.text = "更新服务器配置"; 
	}
	elseif(idx==2) {  
		winform.btnUpdate.text = "更新内核配置 " 
	}	
	elseif(idx==3) { 
		winform.subscription.loadSubscriptionsOnce();
	}
}
tbs.selIndex = 1;

import style;
winform.btnUpdate.skin(style.primaryButton);
winform.btnUpdate.oncommand = function(id,event){
	winform.btnUpdate.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};

	if(tbs.selIndex==1){
		if(..string.match(winform.editOutbounds.text,"%\[\]")){
			var cfg,err = web.json.tryParse(winform.editOutbounds.text);
			if(!cfg){
				winform.editOutbounds.showErrorTip(,string.concat("JSON语法错误 ",err));
				winform.btnUpdate.disabledText = null;
				return;
			}
			
			if(cfg){
				cfg = xray.outbounds.validAll(cfg)	
			}
			config.proxy.outbounds = cfg : ..table.array();
		}
		else {
			var outbounds = xray.outbounds.importFromString(winform.editOutbounds.text);
			config.proxy.outbounds = outbounds : ..table.array(); 
			winform.editOutbounds.text = web.json.stringifyArray(outbounds,true,false,true);
		}
		
		config.proxy.save();
		winform.editOutbounds.modified = false;
		publish("uiCommand.restartxrayCore",cfg)    	
	}
	elseif(tbs.selIndex==2) {
		var cfg,err = web.json.tryParse(winform.editCoreConfig.text);
		if(type(cfg)!==type.table){
			winform.editCoreConfig.showErrorTip(,string.concat("JSON语法错误 ",err));
			winform.btnUpdate.disabledText = null;
			return;
		}
		
		var proxyOutboundInCore;
		for(k,ob in cfg.outbounds){
			if(ob.tag == "proxy"){
				proxyOutboundInCore = ob;
			} 
		}
		
		if( type(proxyOutboundInCore) !="table"  ){
			winform.msgErr(`outbounds 数组中未找到默认出站节点 {tag:"proxy"}`);
			winform.btnUpdate.disabledText = null;
			return; 
		}
		
		var socksInbounds,httpInbounds;
		for(k,ib in cfg.inbounds){
			if(ib.tag == "proxy"){
				socksInbounds = ib;
			} 
			elseif(ib.tag == "http_proxy"){
				httpInbounds = ib;
			} 
		}
			
		if( type(socksInbounds) !="table"  ){
			winform.msgErr(`inbounds 数组中未找到SOCKS协议入站节点 {tag:"proxy"}`);
			winform.btnUpdate.disabledText = null;
			return; 
		}
		
		if( type(httpInbounds) !="table"  ){
			winform.msgErr(`inbounds 数组中未找到HTTP协议入站节点 {tag:"http_proxy"}`);
			winform.btnUpdate.disabledText = null;
			return; 
		}
	
		if(type(socksInbounds[["listen"]])!="string"){
			winform.msgErr(`出站节点 {tag:"proxy"} listen字段必须指定IP或0.0.0.0`);
			winform.btnUpdate.disabledText = null;
			return; 
		}

		if(type(socksInbounds[["port"]])!="number"){
			winform.msgErr(`出站节点 {tag:"proxy"} port字段必须用数值指定监听端口`);
			winform.btnUpdate.disabledText = null;
			return; 
		} 

		if(type(httpInbounds[["listen"]])!="string"){
			winform.msgErr(`出站节点 {tag:"http_proxy"} listen字段必须指定IP或0.0.0.0`);
			winform.btnUpdate.disabledText = null;
			return; 
		}

		if(type(httpInbounds[["port"]])!="number"){ 
			winform.msgErr(`出站节点 {tag:"http_proxy"} port字段必须用数值指定监听端口`);
			winform.btnUpdate.disabledText = null;
			return; 
		} 
		
		config.core.default = cfg;
		config.core.save();
		
		publish("uiCommand.restartxrayCore",cfg);   
		publish("config.core.changed");
	} 

	win.delay(500);
	winform.btnUpdate.disabledText = null;
	winform.msgOk("配置已更新",1200);
}


import xray.outbounds;
winform.editOutbounds.enablePopMenu(function(){
	if(tbs.selIndex==1){
		var strEditBounds = string.trim(winform.editOutbounds.text);
		var menu = {
			{ /*---分隔线---*/ };
			{ "自剪贴板导入 vmess/vless/trojan/ss 链接或订阅源";
				function(id){ 
					
					var outbounds = xray.outbounds.importFromClipboard();
					if(#outbounds){ 
						var json = strEditBounds;
						if( (!#json) || ..string.match(json,"%\[\]")){
							var cfgOutbounds,err = ..table.array();
							if(#json){
								cfgOutbounds,err= web.json.tryParse(strEditBounds);
								if(!cfgOutbounds){
									winform.editOutbounds.showErrorTip(,string.concat("当前配置存在JSON语法错误 ",err));
									return;
								}	
							} 
							
							var subscribeUrl = outbounds[1].subscribeUrl;
							if(subscribeUrl){
								for(i=#cfgOutbounds;1;-1){
									var outbound = cfgOutbounds[i] 
									if(outbound.subscribeUrl==url){
										..table.remove(	cfgOutbounds,i );
									}
								}
							}
							
							..table.append(cfgOutbounds,outbounds);
							winform.editOutbounds.text = ..web.json.stringifyArray(cfgOutbounds,true,false,true);
						}
						else {
							var str = xray.outbounds.exportSharedLinks(outbounds);
							if(#strEditBounds){
								winform.editOutbounds.text = strEditBounds+ '\r\n' + str;
							}
							else {
								winform.editOutbounds.text = str;
							} 
						}
						
						winform.msgOk("已成功导入" + #outbounds + "个服务器,请点击「更新设置」启用新配置。",1200);
						winform.editOutbounds.modified = true;
						winform.editOutbounds.setFocus();
						return; 
					}
		
					winform.msgFrown('未导入服务器！\r\n请先复制以下格式文本（自动清除其中的无效内容）：\r\n\r\n1、一行或多行（忽略无效行）分享链接或服务器JSON配置。\r\n支持 vmess://，vless://，ss://，ssr://，trojan://， trojan-go:// 等通用分享链接。\r\n\r\n2、包含多个服务器配置的JSON数组。\r\n\r\n3、单个 http:// 或 https:// 开头的通用订阅源地址。\r\n订阅源地址支持直接使用github项目文件地址（网址含raw或blob目录名）。\r\n订阅源可以BASE64编码或明文返回以上1、2条规定的配置或分享链接。')
				} 
			};  
		}
		
		if( (!#strEditBounds) || ..string.match(strEditBounds,"%\[\]")){
			menu[3] = { "更新当前配置内订阅源";
				function(id){ 
					var cfg,err = ..table.array();
					
					var json = strEditBounds;
					if(#json){
						cfg,err = web.json.tryParse(strEditBounds);
						if(!cfg){
							winform.editOutbounds.showErrorTip(,string.concat("JSON语法错误 ",err));
							return;
						}
					}
						
					var subscribeUrls = {}
					for(i=#cfg;1;-1){
						var outbound = cfg[i] 
						if(outbound.subscribeUrl){
							subscribeUrls[outbound.subscribeUrl] = true;	
						}
					}
					
					if(!table.count(subscribeUrls)){
						import win.clip;
						var clibStr = win.clip.read();
						if( ..string.startWith(clibStr,"http://") || ..string.startWith(clibStr,"https://")  ){
							subscribeUrls[clibStr] = true;
						}
						else {
							winform.msgWarn('当前配置不包含来自订阅源的服务器,\n请先复制订阅源网址到剪贴板!');
							return;
						} 
					}
						
					var count = 0;
					for(url,v in subscribeUrls){ 
						var outbounds = xray.outbounds.importFromString(url);
						if(#outbounds){
							for(i=#cfg;1;-1){
								var outbound = cfg[i] 
								if(outbound.subscribeUrl==url){
									..table.remove(	cfg,i );
								}
							}
							
							count = count + #outbounds;
							..table.append(cfg,outbounds);
						}
					}
					
					winform.editOutbounds.text = ..web.json.stringifyArray(cfg,true,false,true);
						
					winform.msgOk("已成功刷新" + count + "个服务器,请点击「更新设置」启用新配置。",1200);
					winform.editOutbounds.modified = true;
					winform.editOutbounds.setFocus();
					return;   
				} 
			};
		}
		
		if(..string.match(strEditBounds,"%\[\]")){	
			menu[4] = {}	
			menu[5] = { "转换为 vmess、vless、trojan、trojan-go、ss 分享链接";
				function(id){ 
					var cfgOutbouds,err = web.json.tryParse(strEditBounds);
					if(!cfgOutbouds){
						winform.editOutbounds.showErrorTip(,string.concat("JSON语法错误 ",err));
						return;
					}
					
					for(i,ob in cfgOutbouds){
						if(ob.subscribeUrl){
							if(!winform.msgAsk('当前配置包含订阅源,\n转换为分享链接所有服务器与订阅源将解除关联,\n按确定继续操作，按取消中止。')){
								return;
							}
							
							break;
						} 
					}
					
					var str = xray.outbounds.exportSharedLinks(cfgOutbouds);
					if(str){ 
						winform.editOutbounds.text = str;
						winform.editOutbounds.modified = true;
					} 
				} 
			}; 
			menu[6] = { "转换为 Base64 订阅源";
				function(id){ 
					var cfg,err = web.json.tryParse(strEditBounds);
					if(!cfg){
						winform.editOutbounds.showErrorTip(,string.concat("JSON语法错误 ",err));
						return;
					}
		
					var str = xray.outbounds.exportSharedLinks(cfg);
					if(str){ 
						import crypt;
						winform.editOutbounds.text = ..crypt.encodeBin(str);
						winform.editOutbounds.modified = true;
					} 
				} 
			};
		}
		elseif( #strEditBounds ) {
			menu[3] = {}
			
			menu[4] = { "转换为 JSON";
				function(id){
					import web.json;
					var outbounds = xray.outbounds.importFromString(strEditBounds);
					if(outbounds){ 
						winform.editOutbounds.text = web.json.stringifyArray(outbounds,true,false,true);
						winform.editOutbounds.modified = true;
					} 
				} 
			}; 
		} 
		
		table.push(menu,{ /*---分隔线---*/ });
		
		table.push(menu,{ "打开配置文件(JSON)";
			function(id){
				import fsys.dlg;
				var path = fsys.dlg.open("*.json|*.json||","打开 json 文件",,winform)
				if(path){
					winform.editOutbounds.text = string.load(path);
					winform.editOutbounds.modified = true;
				}
			} 
		}); 
		
		table.push(menu,{ "另存为配置文件(JSON)";
			function(id){
				import fsys.dlg;
				var path = fsys.dlg.save("*.json|*.json||","另存为 json 文件",,winform)
				if(path){
					var str = web.json.stringifyArray( 
						config.proxy.outbounds,true,false,true
					)
					
					string.save(path,str )
				}
			} 
		}); 
		
		table.push(menu,{ "另存为默认服务器列表(JSON)";
			function(id){
				var str = web.json.stringifyArray( 
					config.proxy.outbounds,true,false,true
				)
				
				string.save("/xray-core/WinXray-default-servers.json",str );
				winform.msgOk("已存为默认服务器列表 /xray-core/WinXray-default-servers.json",1000)
			} 
		}); 
				
		table.push(menu,{ /*---分隔线---*/ });
		if(winform.editOutbounds.modified){
			table.push(menu,{ "恢复为当前使用的服务器列表(JSON)";
				function(id){
					winform.btnUpdate.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
					winform.editOutbounds.text = web.json.stringifyArray( 
						config.proxy.outbounds,true,false,true
					)
					winform.editOutbounds.modified = false;
					
					win.delay(500);
					winform.btnUpdate.disabledText = null;
				} 
			}); 
		}
		
		table.push(menu,{ "重置为默认服务器列表(JSON)";
			function(id){
				winform.btnUpdate.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
				..config.__loadDefaultOutbounds();
				winform.editOutbounds.modified = false;
				publish("uiCommand.restartxrayCore",cfg);
				
				winform.btnUpdate.disabledText = null;
			} 
		}); 
		
		table.push(menu,{ "清空服务器列表";
			function(id){
				winform.editOutbounds.text = "[]";
				winform.editOutbounds.modified = true;
			} 
		}); 
		
		if(  ..string.match(winform.editOutbounds.text,"%\[\]")){

		table.push(menu,{ /*---分隔线---*/ });
			table.push(menu,{ "插入配置字段";{
				{ "代理服务协议:protocol";
					function(id){
						winform.editOutbounds.selText = '"protocol":"vless",'
					} 
				};
				{ "SSH管理端口:sshPort";
					function(id){
						winform.editOutbounds.selText = '"sshPort":22,'
					} 
				};
				{ "代理服务器地址:address";
					function(id){
						winform.editOutbounds.selText = '"address":"",'
					} 
				};
				{ "端口:port";
					function(id){
						winform.editOutbounds.selText = '"port":443,'
					} 
				};
				{ "网络协议:network";
					function(id){
						winform.editOutbounds.selText = '"network":"tcp",'
					} 
				};
				{ "服务器密码:id";
					function(id){
						winform.editOutbounds.selText = '"id":"",'
					} 
				};
				{ "加密方式:security";
					function(id){
						winform.editOutbounds.selText = '"security":"auto",'
					} 
				};
				{ "允许忽略证书:allowInsecure";
					function(id){
						winform.editOutbounds.selText = '"allowInsecure":true,'
					} 
				};
				{ "TLS服务器名:sni";
					function(id){
						winform.editOutbounds.selText = '"sni":"",'
					} 
				};
				{ "启用TLS:tls";
					function(id){
						winform.editOutbounds.selText = '"tls":"tls",'
					} 
				};
				{ "流控:flow";
					function(id){
						winform.editOutbounds.selText = '"flow":"xtls-rprx-direct",'
					} 
				};
				{ "多路复用最大连接:concurrency";
					function(id){
						winform.editOutbounds.selText = '"concurrency":4,'
					} 
				};
		
				{ "伪装类型:type";
					function(id){
						winform.editOutbounds.selText = '"type":"http",'
					} 
				};
				{ "备注:ps";
					function(id){
						winform.editOutbounds.selText = '"ps":"",'
					} 
				};
				{ "请求主机名:host";
					function(id){
						winform.editOutbounds.selText = '"host":"",'
					} 
				};
				{ "请求路径:path";
					function(id){
						winform.editOutbounds.selText = '"path":"/",'
					} 
				};
				{ "HTTP请求方法:httpMethod";
					function(id){
						winform.editOutbounds.selText = '"httpMethod":"GET",'
					} 
				};
				{ "HTTP请求头:headers";
					function(id){
						winform.editOutbounds.selText = '"headers":{"method":"GET"},'
					} 
				};
				{ "绑定的订阅网址:subscribeUrl";
					function(id){
						winform.editOutbounds.selText = '"subscribeUrl":"https://",'
					} 
				};
				{ "允许自动测试并连接:autoConnect";
					function(id){
						winform.editOutbounds.selText = '"autoConnect":false,'
					} 
				};
				{ "备注:ps";
					function(id){
						winform.editOutbounds.selText = '"ps":"",'
					} 
				};
			}}); 
		}
		
		return menu;
	} 
})

winform.editCoreConfig.enablePopMenu({ 
	{ /*---分隔线---*/ };
	{ "重置为默认内核配置";
		function(id){  
			winform.editCoreConfig.text = string.crlf( string.load("\forms\main\config\coreConfigs\default.json") )
			winform.editCoreConfig.msgOk('已重置为默认内核配置。\n请在下面点击：『更新内核配置』启用新的配置。',1300)
		} 
	}; 
	{ "重置为启用路由规则默认内核配置";
		function(id){  
			winform.editCoreConfig.text = string.crlf( string.load("\forms\main\config\coreConfigs\xray.rules.json") )
			import xray.core.rules;
			var versionTag = xray.core.rules.check();
			if(versionTag) winform.editCoreConfig.msgOk('已重置为启用路由规则默认内核配置，\n已下载路由规则数据最新版本：' + versionTag+ '\n\n请在下面点击：『更新内核配置』启用新的配置。',2000)
			else winform.msgOk('已重置为启用路由规则默认内核配置，\n请在下面点击：『更新内核配置』启用新的配置。',1300)
		} 
	};  
	{ "恢复到当前正在使用的内核配置";
		function(id){   
			winform.editCoreConfig.text = web.json.stringify( 
				config.core.default,true
			)

			winform.editCoreConfig.msgOk('已恢复到当前正在使用的内核配置',1300)
		} 
	}; 
} )

subscribe("outbounds.updateConfigJson",function(){
	if(winform.editOutbounds.modified){
		return;
	}
	
	winform.editOutbounds.text = web.json.stringifyArray( 
		config.proxy.outbounds,true,false,true
	)
	
	winform.editOutbounds.modified = false;
} )

winform.editOutbounds.text = web.json.stringifyArray( 
		config.proxy.outbounds,true,false,true
) : "[]"
winform.editOutbounds.modified = false;

subscribe("config.inbounds.changed",function(){
	if(winform.editCoreConfig.modified){
		return;
	}
	
	winform.editCoreConfig.text = web.json.stringify( 
		config.core.default,true
	)
	
	winform.editCoreConfig.modified = false;
} )
winform.editCoreConfig.text = web.json.stringify( 
	config.core.default,true
)

var editOutboundsTip = {
	address = {"代理服务器地址";"值可以是域名或者IP地址"};
	alterId = {"额外ID";"仅用于VMESS协议，与服务器一致即可"};
	aid = {"额外ID(alterId)";"数值,仅用于VMESS协议，与服务器一致即可"};
	network = {"传输协议(network)";"可选值为tcp,ws等，也可以放 SSR 的 protocol 值"};
	net = {"传输协议(network)";"可选值为tcp,ws等"};
	type = {"伪装类型";"默认的可选值为none,http等,kcp或quic此字段表示header.type，可选值为 none,srtp,utp,wechat-video,dtls,wireguard"}; 
	ps = {"备注";"备注"}; 
	tls = {"是否启用TLS";"可选值为tls,xtls或空值,vless协议省略时默认值为tls"}; 
	path = {"请求路径";"用于ws,http等协议指定请求路径"}; 
	httpMethod = {"HTTP请求方法";"用于http协议指定请求方法"}; 
	headers = {"HTTP请求头";"用于ws,http等协议指定请求头"}; 
	id = {"密码";"服务器密码"}; 
	host = {"主机名";"用于指定http,ws等协议请求头中的主机名"}; 
	protocol = {"协议";"可选值为 vmess,vless,shadowsocks,trojan,trojan-go 之一"}; 
	port = {"端口";"服务器端口,数值"}; 
	security = {"加密方式";"可省略,vmess协议默认为auto,vless协议默认为none"};
	sni = {"TLS服务器名";"TLS客户端请求中的服务器名字段,如果不指定则默认取host或address字段值。"}; 
	flow = {"流控";"目前仅用于选择 XTLS 的算法,省略时默认值为xtls-rprx-direct"}; 
	obfs = {"混淆插件";"可选值：plain,http_simple,http_post,tls1.2_ticket_auth ..."}; 
	obfsParam = {"混淆参数";"SSR 里的 obfs_param"}; 
	networkParam = {"SSR网络协议参数";"SSR 里的 protocol_param"}; 
	concurrency = {"Mux多路复用:最大并发连接数";"最小值1，最大值1024，特殊值-1，不加载mux模块。缺省由WinXray根据连接协议自动设定此值。"}; 
}

winform.editOutbounds.wndproc = function(hwnd,message,wParam,lParam){
	if(message == 0x202/*_WM_LBUTTONUP*/){
		var lineIndex = winform.editOutbounds.lineFromChar();
		if(lineIndex == winform.editOutbounds.preLineIndex){
			return; 
		}
		winform.editOutbounds.preLineIndex = lineIndex;
		
		var line = winform.editOutbounds.lineText();
		var n,v = string.match(line,`^\s*\"(\w+)\"\s*\:\s*(%"")[\,\s]*$`); 
		if( !n ) n,v = string.match(line,`^\s*\"(\w+)\"\s*\:\s*(\d+)[\,\s]*$`);
		if( !n ) n,v = string.match(line,`^\s*\"(\w+)\"\s*\:\s*\{`);
		if(editOutboundsTip[n]){
			winform.editOutbounds.showInfoTip(editOutboundsTip[n][1],editOutboundsTip[n][2])
		}
	} 
}

winform.editCoreConfig.oncommand = function(id,event){
	if( event === 0x100/*_EN_SETFOCUS*/){
		winform.lbTipForCoreJson.hide = false;
	}
}

winform.editOutbounds.limit = 0;
winform.editCoreConfig.limit = 0;

winform.btnUpdateSsrCore.skin(style.plainButton)
winform.btnUpdateSsrCore.oncommand = function(id,event){
	winform.btnUpdateSsrCore.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	
	import xray.core.ssr;
	var versionTag = xray.core.ssr.updateCore();
	if( versionTag ){
		publish("uiCommand.restartxrayCore");
		winform.btnUpdateSsrCore.disabledText = null;
		winform.msgOk("SSR Core（ ShadowsocksR-native ） 已更新到：" + versionTag,1500)
	}
	else {
		winform.btnUpdateSsrCore.disabledText = null;
	} 	
}

winform.btnUpdatexrayCore.skin(style.plainButton)
winform.btnUpdatexrayCore.oncommand = function(id,event){
    winform.btnUpdatev2rayCore.disabled = true;
	winform.btnUpdatexrayCore.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	
	import xray.core;
	var versionTag = xray.core.updateCore();
	if( versionTag ){
		publish("uiCommand.restartxrayCore");
		winform.btnUpdatexrayCore.disabledText = null;
		winform.msgOk("已下载最新 xray Core 版本：" + versionTag + '\nxray 内核已切换为： xray Core',1500)
	}
	else {
		winform.btnUpdatexrayCore.disabledText = null;
		winform.msgErr("xray Core 下载失败！");
	} 
	
	winform.btnUpdatev2rayCore.disabled = false;
}

winform.btnUpdatev2rayCore.skin(style.plainButton)
winform.btnUpdatev2rayCore.oncommand = function(id,event){
    
    winform.btnUpdatexrayCore.disabled = true;
	winform.btnUpdatev2rayCore.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	
	import xray.core.v2ray;
	var versionTag = xray.core.v2ray.updateCore();
	if( versionTag ){
		publish("uiCommand.restartxrayCore");
		winform.btnUpdatev2rayCore.disabledText = null;
		winform.msgOk("已下载最新 v2ray Core 版本：" + versionTag+ '\nxray 内核已切换为：v2ray Core',1500)
	}
	else {
		winform.btnUpdatev2rayCore.disabledText = null;
		winform.msgErr("v2ray Core 下载失败！");
	} 
	
	winform.btnUpdatexrayCore.disabled = false;
}

winform.btnUpdateRules.skin(style.plainButton)
winform.btnUpdateRules.oncommand = function(id,event){
     
	winform.btnUpdateRules.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	 
	import xray.core.rules;
	var versionTag = xray.core.rules.update();
	if( versionTag ){
		publish("uiCommand.restartxrayCore");
		winform.btnUpdateRules.disabledText = null;
		winform.msgOk('已下载路由规则到最新版本：' + versionTag+ '\n请在下面的配置编辑器点击右键,然后点击：\n『重置为启用路由规则默认内核配置』',2000)
	}
	else {
		winform.btnUpdateRules.disabledText = null;
		winform.msgErr("路由规则下载失败！");
	}  
}

winform.btnUpdateNaiveCore.skin(style.plainButton)
winform.btnUpdateNaiveCore.oncommand = function(id,event){
	winform.btnUpdateNaiveCore.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	
	import xray.core.naive;
	var versionTag = xray.core.naive.updateCore();
	if( versionTag ){
		publish("uiCommand.restartxrayCore");
		winform.btnUpdateNaiveCore.disabledText = null;
		winform.msgOk("NaÏveProxy Core 已更新到：" + versionTag,1500)
	}
	else {
		winform.btnUpdateNaiveCore.disabledText = null;
		winform.msgErr("NaÏveProxy Core 下载失败！");
	} 		
}

if(!_WIN10_LATER){
    winform.navUwpExemption.close();
}
else {
	winform.navUwpExemption.skin(style.plainButton)
	winform.navUwpExemption.oncommand = function( id,event ){
		if(_STUDIO_INVOKED){
        	winform.msgErr("请先发布为 EXE！");
        	return;
    	}
    	
    	import process;
    	process.execute(io._exepath,"/uwp","runas")
	}
}

winform.show();
win.loopMessage();
return winform;