import web.json;
import win.dlg.message;
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="WinXray - 代理服务器配置";right=999;bottom=504;bgcolor=16777215;exmode="none";min=false;mode="popup")
winform.add(
btnAddOutbound={cls="plus";text="新 增";left=705;top=410;right=808;bottom=446;align="left";bgcolor=14935259;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=22}};iconText='\uF067';notify=1;textPadding={left=40};z=17};
btnCopySharedLink={cls="plus";text="复制";left=829;top=471;right=901;bottom=496;align="left";clip=1;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF0C1';notify=1;textPadding={left=30};z=1};
btnGenGuid={cls="plus";text="生成";left=914;top=259;right=986;bottom=284;align="left";clip=1;dr=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF0AD';notify=1;textPadding={left=30};z=19};
btnImportSharedLink={cls="plus";text="导入";left=912;top=471;right=984;bottom=496;align="left";clip=1;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF0C1';notify=1;textPadding={left=30};z=21};
btnInsertField={cls="plus";text="插入更多配置字段";left=10;top=0;right=191;bottom=24;align="left";dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF196 ';notify=1;textPadding={left=30};z=18};
btnSaveOutbound={cls="plus";text="保 存";left=826;top=410;right=929;bottom=446;align="left";bgcolor=11580047;db=1;disabled=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=22}};iconText='\uF0C7';notify=1;textPadding={left=40};z=16};
btnUpdateCore={cls="plus";text="下载 / 更新 NaÏveProxy Core";left=637;top=61;right=979;bottom=89;align="left";color=3947580;dr=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-17;name='FontAwesome');padding={left=18}};iconText='\uF0AB';notify=1;textPadding={left=50};z=23};
cmbNetwork={cls="combobox";left=656;top=97;right=814;bottom=123;dr=1;dt=1;edge=1;items={"tcp";"kcp";"ws";"h2";"quic"};mode="dropdown";z=13};
cmbProtocol={cls="combobox";left=656;top=28;right=814;bottom=54;dr=1;dt=1;edge=1;items={"vmess";"vless";"trojan";"trojan-go";"ssr";"shadowsocks";"naive";"socks";"https";"http"};mode="dropdown";z=12};
cmbSecurity={cls="combobox";left=656;top=140;right=814;bottom=166;dr=1;dt=1;edge=1;items={"auto";"none";"chacha20-poly1305";"aes-128-gcm"};mode="dropdown";z=15};
editAddress={cls="plus";left=656;top=183;right=878;bottom=207;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};dr=1;dt=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=2};
editId={cls="plus";left=656;top=263;right=986;bottom=287;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};clip=1;dr=1;dt=1;editable="edit";font=LOGFONT(h=-16);notify=1;textPadding={right=75;bottom=1};z=20};
editOutbound={cls="edit";left=15;top=25;right=472;bottom=462;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-13);hscroll=1;multiline=1;vscroll=1;z=14};
editPort={cls="plus";left=656;top=224;right=745;bottom=248;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};dr=1;dt=1;editable="edit";font=LOGFONT(h=-16);num=1;textPadding={bottom=1};z=4};
editPs={cls="plus";left=656;top=306;right=878;bottom=330;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};dr=1;dt=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=7};
editSharedLink={cls="plus";left=13;top=475;right=986;bottom=499;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};clip=1;db=1;dl=1;dr=1;font=LOGFONT(h=-16);notify=1;textPadding={right=150;bottom=1};z=22};
lbId={cls="static";text="登录用户( id ):";left=492;top=262;right=641;bottom=288;align="right";bgcolor=16777215;dr=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=6};
lbSecurity={cls="static";text="加密方式( security ):";left=473;top=136;right=641;bottom=162;align="right";bgcolor=16777215;dl=1;dr=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=10};
qrPicture={cls="plus";left=510;top=358;right=606;bottom=454;bgcolor=16777215;db=1;dr=1;notify=1;repeat="scale";z=24};
static={cls="static";text="服务器( address ):";left=481;top=178;right=641;bottom=204;align="right";bgcolor=16777215;dl=1;dr=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=3};
static10={cls="static";text="代理协议( protocol ):";left=473;top=26;right=641;bottom=52;align="right";bgcolor=16777215;dl=1;dr=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=11};
static2={cls="static";text="服务端口( port ):";left=498;top=220;right=641;bottom=246;align="right";bgcolor=16777215;dr=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=5};
static4={cls="static";text="备注( ps ):";left=498;top=304;right=641;bottom=330;align="right";bgcolor=16777215;dr=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=8};
static5={cls="static";text="传输协议( network ):";left=473;top=94;right=641;bottom=126;align="right";bgcolor=16777215;dl=1;dr=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=9}
)
/*}}*/

winform.outboundConfig = {};
winform.addOutboundField = function(k,v){
	if(winform.outboundConfig[k] !== null){
		return winform.msgInfo("该字段已经存在，不需要添加！"); 
	}
	
	winform.outboundConfig[k] = v;
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
	winform.updateSharedLink();
}

winform.cmbProtocol.onProtocolChange = function(){ 
	var sel = winform.cmbProtocol.selText;
	if(sel=="vmess"){
		winform.cmbSecurity.items = {
			"auto";"none";"chacha20-poly1305";"aes-128-gcm"
		}
		winform.cmbSecurity.selIndex = 1;
		winform.outboundConfig.security = "auto";
		
		winform.cmbNetwork.items = {
			"tcp";"ws";"kcp";"h2";"quic";"grpc";
		}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "tcp";
		winform.btnUpdateCore.text = '下载 / 更新支持组件： Xray Core';
		winform.editId.setCueBannerText("请输入 GUID");
	}
	elseif(sel=="shadowsocks"){
		winform.cmbSecurity.items = {
			"aes-256-gcm";"aes-128-gcm";"chacha20-poly1305";"chacha20-ietf-poly1305";"none"
		}

		winform.cmbSecurity.selIndex = 1;
		winform.outboundConfig.security = "aes-256-gcm";
		
		winform.cmbNetwork.items = {"tcp";}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "tcp";
		winform.btnUpdateCore.text = '下载 / 更新支持组件： Xray Core';
		winform.editId.setCueBannerText("请输入密码");
	}
	elseif(sel=="ssr"){
		winform.cmbNetwork.items = {
			"origin";"plain";"auth_sha1_v4";"http_simple";"auth_aes128_sha1";"http_post";"auth_aes128_md5";"http_mix";"auth_chain_a";"tls1.2_ticket_auth";"auth_chain_b";"tls1.2_ticket_fastauth";"auth_chain_c/d/e/f";
		}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "origin";
		
		winform.cmbSecurity.items = {
			"none";"table";"rc4";"rc4-md5-6";"rc4-md5";"aes-128-cfb";"aes-192-cfb";"aes-256-cfb";"aes-128-ctr";"aes-192-ctr";"aes-256-ctr";"camellia-128-cfb";"camellia-192-cfb";"camellia-256-cfb";"bf-cfb";"cast5-cfb";"des-cfb";"idea-cfb";"rc2-cfb";"seed-cfb";"salsa20";"chacha20";"chacha20-ietf";		
		}

		winform.cmbSecurity.selIndex = 1;
		winform.outboundConfig.security = "none";  
		winform.btnUpdateCore.text = '下载 / 更新支持组件： SSR Core';
		winform.editId.setCueBannerText("请输入密码");
	}
	elseif(sel=="vless"){
		winform.cmbSecurity.items = {
			"none";
		}
		winform.cmbSecurity.selIndex = 1;
		winform.outboundConfig.security = "none";
		
		winform.cmbNetwork.items = {
			"tcp";"ws";"kcp";"h2";"quic";"grpc";
		}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "tcp";
		winform.btnUpdateCore.text = '下载 / 更新支持组件： V2ray Core';
		winform.editId.setCueBannerText("请输入 GUID");
	}
	elseif(sel=="naive"){
		winform.cmbNetwork.items = {
			"https";"quic";
		}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "https";
		
		winform.cmbSecurity.items = {}
		winform.outboundConfig.security = null;  
		 
		winform.btnUpdateCore.text = '下载 / 更新支持组件： NaïveProxy Core';
		winform.editId.setCueBannerText("请输入 用户名:密码");
	}
	elseif(sel=="socks"){
		winform.cmbSecurity.items = { }	
		winform.outboundConfig.security = null;
		
		winform.cmbNetwork.items = {"tcp";}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "tcp";  
		winform.btnUpdateCore.text = '下载 / 更新支持组件： Xray Core';
		winform.editId.setCueBannerText("请输入 用户名:密码");
	}
	elseif(sel=="http" || sel=="https"){
		winform.cmbSecurity.items = { }	
		winform.outboundConfig.security = null;
		
		winform.cmbNetwork.items = {"tcp";}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "tcp";  
		winform.btnUpdateCore.text = '下载 / 更新支持组件： Xray Core';
		winform.editId.setCueBannerText("请输入 用户名:密码");
	}
	else {
		winform.cmbSecurity.items = { }	
		winform.outboundConfig.security = null;
		
		winform.cmbNetwork.items = {"tcp";}
		winform.cmbNetwork.selIndex = 1;
		winform.outboundConfig.network = "tcp";
		winform.btnUpdateCore.text = '下载 / 更新支持组件： Xray Core';
		winform.editId.setCueBannerText("请输入密码");
	} 
}

winform.cmbProtocol.onListChange = function(){ 
	winform.cmbProtocol.onProtocolChange(); 

	table.define(winform.outboundConfig);
	winform.outboundConfig.protocol = winform.cmbProtocol.selText;;
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
	winform.updateSharedLink();
}

winform.cmbNetwork.onListChange = function(){ 
	var sel = winform.cmbNetwork.selText;
	winform.outboundConfig.network = sel;
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);	
	winform.updateSharedLink();
}

winform.cmbSecurity.onListChange = function(){ 
	var sel = winform.cmbSecurity.selText;
	winform.outboundConfig.security = sel;
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);	
	winform.updateSharedLink();
}
	
winform.editId.editBox.onChange = function(){
    if( winform.outboundConfig.id == winform.editId.text ){
    	return;
    }
      
	winform.outboundConfig.id = winform.editId.text; 
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
	winform.updateSharedLink();
}

winform.editPort.editBox.onChange = function(){  
	winform.outboundConfig.port = tonumber(owner.text):443;
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
	winform.updateSharedLink();
}

winform.editAddress.editBox.onChange = function(){  
	winform.outboundConfig.address = owner.text;
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
	winform.updateSharedLink();
}

winform.editPs.editBox.onChange = function(){  
	winform.outboundConfig.ps = owner.text;
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
	winform.updateSharedLink();
}

var editOutboundTip = {
	address = {"代理服务器地址";"值可以是域名或者IP地址"};
	alterId = {"额外ID";"仅用于VMESS协议，与服务器一致即可"};
	aid = {"额外ID(alterId)";"数值,仅用于VMESS协议，与服务器一致即可"};
	network = {"传输协议(network)";"可选值为tcp,ws等，也可以放 SSR 的 protocol 值"};
	net = {"传输协议(network)";"可选值为tcp,ws等"};
	type = {"伪装类型";"默认的可选值为none,http等,kcp或quic此字段表示header.type，可选值为 none,srtp,utp,wechat-video,dtls,wireguard"}; 
	ps = {"备注";"备注"}; 
	tls = {"是否启用TLS";"可选值为tls,xtls或空值,vless协议省略时默认值为tls"}; 
	path = {"请求路径";"用于ws,http等协议指定请求路径"}; 
	httpMethod = {"HTTP请求方法";"用于http协议指定请求方法"}; 
	headers = {"HTTP请求头";"用于ws,http等协议指定请求头"}; 
	id = {"密码";"服务器密码"}; 
	host = {"主机名";"用于指定http,ws等协议请求头中的主机名"}; 
	protocol = {"协议";"可选值为 vmess,vless,shadowsocks,trojan,trojan-go 之一"}; 
	port = {"端口";"服务器端口,数值"}; 
	security = {"加密方式";"可省略,vmess协议默认为auto,vless协议默认为none"};
	sni = {"TLS服务器名";"TLS客户端请求中的服务器名字段,如果不指定则默认取host或address字段值。"}; 
	flow = {"流控";"目前仅用于选择 XTLS 的算法,省略时默认值为xtls-rprx-direct"}; 
	grpcSni = {"gRPC的ServiceName";"gRPC传输方式的ServiceName"};
	obfs = {"混淆插件";"可选值：plain,http_simple,http_post,tls1.2_ticket_auth ..."}; 
	obfsParam = {"混淆参数";"SSR 里的 obfs_param"}; 
	networkParam = {"SSR网络协议参数";"SSR 里的 protocol_param"}; 
	concurrency = {"Mux多路复用:最大并发连接数";"最小值1，最大值1024，特殊值-1，不加载mux模块。缺省由WinXray根据连接协议自动设定此值。"}; 
}

winform.editOutbound.wndproc = function(hwnd,message,wParam,lParam){
	if(message == 0x202/*_WM_LBUTTONUP*/){
		var lineIndex = winform.editOutbound.lineFromChar();
		if(lineIndex == winform.editOutbound.preLineIndex){
			return; 
		}
		winform.editOutbound.preLineIndex = lineIndex;
		
		var line = winform.editOutbound.lineText();
		var n,v = string.match(line,`^\s*\"(\w+)\"\s*\:\s*(%"")[\,\s]*$`); 
		if( !n ) n,v = string.match(line,`^\s*\"(\w+)\"\s*\:\s*(\d+)[\,\s]*$`);
		if( !n ) n,v = string.match(line,`^\s*\"(\w+)\"\s*\:\s*\{`);
		if(editOutboundTip[n]){
			winform.editOutbound.showInfoTip(editOutboundTip[n][1],editOutboundTip[n][2])
		}
	} 
}
	
import win.ui.menu;
var insertFieldMenu = { 
	{ "允许忽略证书:allowInsecure";
		function(id){
			winform.addOutboundField("allowInsecure",true);
		} 
	};
	{ "TLS服务器名:sni";
		function(id){
			winform.addOutboundField("sni","");
		} 
	};
	{ "启用TLS:tls";
		function(id){
			winform.addOutboundField("tls","xtls");
		} 
	};
	{ "流控:flow";
		function(id){
			winform.addOutboundField("flow","xtls-rprx-direct");
		} 
	};
	{ "多路复用最大连接:concurrency";
		function(id){
			winform.addOutboundField("concurrency",4);
		} 
	};

	{ "伪装类型:type";
		function(id){
			winform.addOutboundField("type","http");
		} 
	};
	{ "备注:ps";
		function(id){
			winform.addOutboundField("ps","");
		} 
	};
	{ "请求主机名:host";
		function(id){
			winform.addOutboundField("host","");
		} 
	};
	{ "请求路径:path";
		function(id){
			winform.addOutboundField("path","/");
		} 
	};
	{ "HTTP请求方法:httpMethod";
		function(id){
			winform.addOutboundField("httpMethod","GET");
		} 
	};
	{ "HTTP请求头:headers";
		function(id){
			winform.addOutboundField("headers",{method="GET"});
		} 
	};
	{ "SS混淆插件:obfs";
		function(id){
			winform.addOutboundField("obfs","plain","");
		} 
	};
	{ "SSR混淆参数:obfsParam";
		function(id){
			winform.addOutboundField("obfsParam","");
		} 
	};
	{ "SSR网络协议参数:networkParam";
		function(id){
			winform.addOutboundField("networkParam","");
		} 
	};
	{ "绑定的订阅网址:subscribeUrl";
		function(id){
			winform.addOutboundField("subscribeUrl","https://"); 
		} 
	};
	{ "允许自动测试并连接:autoConnect";
		function(id){
			winform.addOutboundField("autoConnect",false); 
		} 
	};
	{ "SSH管理端口:sshPort";
		function(id){
			winform.addOutboundField("sshPort",22);
		} 
	}; 
} 
winform.editOutbound.enablePopMenu({
	{ "插入配置字段";insertFieldMenu}
})

import style;
winform.btnInsertField.skin(style.transButton)
winform.btnInsertField.oncommand = function(id,event){
	var popInsertFieldMenu = win.ui.popmenu(winform);
	popInsertFieldMenu.addTable(insertFieldMenu);
	
	var x,y,cx,cy = winform.btnInsertField.getPos();
	popInsertFieldMenu.popup(x,y+cy);
}

import win.debounce;
var onEditChange = win.debounce(
	function(){
		var data,err = web.json.tryParse(winform.editOutbound.text); 
		if(!data){
			if(err){
				return winform.editOutbound.showErrorTip("JSON 语法错误",err)	
			}
			return;
		}
		
		var editPortChange = winform.editPort.editBox.onChange;
		var editIdChange = winform.editId.editBox.onChange;
		var editAddressChange = winform.editAddress.editBox.onChange;
		var editPsChange = winform.editPs.editBox.onChange;
		
		winform.editPort.editBox.onChange = null;
		winform.editId.editBox.onChange = null;
		winform.editAddress.editBox.onChange = null;
		winform.editPs.editBox.onChange = null;
		
		winform.editPort.text = data.port;
		winform.editId.text = data.id;
		winform.editAddress.text = data.address;
		winform.editPs.text = data.ps;
		
		winform.editPort.editBox.onChange = editPortChange;
		winform.editId.editBox.onChange = editIdChange;
		winform.editAddress.editBox.onChange = editAddressChange;
		winform.editPs.editBox.onChange = editPsChange;
		
		var cmbNetworkChange = winform.cmbNetwork.onListChange;
		var cmbProtocolChange = winform.cmbProtocol.onListChange;
		var cmbSecurityChange = winform.cmbSecurity.onListChange;
		
		winform.cmbNetwork.onListChange = null;
		winform.cmbProtocol.onListChange = null;
		winform.cmbSecurity.onListChange = null;
		
		winform.cmbNetwork.selText = data.network;
		winform.cmbProtocol.selText = data.protocol;
		winform.cmbProtocol.onProtocolChange();
		winform.cmbSecurity.selText = data.security;
		
		winform.cmbNetwork.onListChange = cmbNetworkChange;
		winform.cmbProtocol.onListChange = cmbProtocolChange;
		winform.cmbSecurity.onListChange = cmbSecurityChange;
		
		winform.updateSharedLink();		
	},500
)	

winform.editOutbound.onChange = function(){  
	onEditChange();
}

winform.btnSaveOutbound.oncommand = function(id,event){
	if(!#winform.editAddress.text){
		return winform.editAddress.editBox.showErrorTip("配置错误","服务器地址不能为空")
	}
	
	if(!#winform.editPort.text){
		return winform.editPort.editBox.showErrorTip("配置错误","端口不能为空")
	}
	
	if( !#winform.editId.text ){
		var protocol = winform.outboundConfig.protocol;
		if((protocol!="socks") && (protocol!="http") && (protocol!="https")){
			return winform.editId.editBox.showErrorTip("配置错误","当前代理协议密码不能为空")
		}  
	}
	
	table.assign(winform.origOutboundConfig,winform.outboundConfig);
	winform.endModal();
	
	publish("uiCommand.restartxrayCore");
	publish("outbounds.updateConfigJson");
	
}

import config;
winform.btnAddOutbound.oncommand = function(id,event){
	if(!#winform.editAddress.text){
		return winform.editAddress.editBox.showErrorTip("配置错误","服务器地址不能为空")
	}
	
	if(!#winform.editPort.text){
		return winform.editPort.editBox.showErrorTip("配置错误","端口不能为空")
	}
	
	if(!#winform.editId.text  ){
		var protocol = winform.outboundConfig.protocol
		if( (protocol!="socks") && (protocol!="http") && (protocol!="https") ){
			return winform.editId.editBox.showErrorTip("配置错误","当前代理协议密码不能为空")
		}
	}
	
	table.push(config.proxy.outbounds,winform.outboundConfig); 
	winform.endModal();

	publish("uiCommand.restartxrayCore");
	publish("outbounds.updateConfigJson"); 
}

import win.guid;
winform.btnGenGuid.oncommand = function(id,event){
    var sel = winform.cmbProtocol.selText;
	if(sel=="socks" || sel=="http" || sel=="https" || sel=="naive"  ){
		winform.editId.text = "user:" + string.random(16);
	}
	elseif(sel=="shadowsocks" || sel=="ssr"  ){
		winform.editId.text = string.random(22,"/@0123456abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ");
	}
	else {
		var guid = win.guid.create();
		winform.editId.text = string.lower( tostring(guid) );	
	}
}
winform.btnGenGuid.skin(style.link)

winform.btnSaveOutbound.skin(style.button)
winform.btnAddOutbound.skin(style.primaryButton)
winform.btnSaveOutbound.disabled = true;

import qrencode.bitmap;
import xray.outbounds;
winform.updateSharedLink = function(modified){ 
	var cfg = winform.outboundConfig;
	var link = "";
	 
	if(#cfg.address && (cfg.port!==null) && #cfg.protocol){
		link = xray.outbounds.exportSharedLinks({winform.outboundConfig});
	}
	
	winform.editSharedLink.text = link;
	
	if(#link){ 
		var qrBmp = qrencode.bitmap(link,0,0);
		if(qrBmp){ 
			winform.qrPicture.setBackground(qrBmp.copyBitmap(winform.qrPicture.width));   
		} 
	}
}

import win.clip;
winform.btnImportSharedLink.skin(style.link);
winform.btnImportSharedLink.oncommand = function(id,event){
	var str = ..win.clip.read();
	if(str){   
		var outbounds = xray.outbounds.importFromString(str);
		if(#outbounds){
			var cfg = winform.outboundConfig;
			table.clear(cfg);
			table.assign(cfg,outbounds[1]);
			
			winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
			onEditChange();
	
			winform.msgOk("已成功导入服务器",1000); 
			return; 
		}
	}

	winform.msgFrown('未导入服务器！\r\n请先复制以下格式文本（自动清除其中的无效内容）：\r\n\r\n1、分享链接或服务器JSON配置。\r\n支持 vmess://，vless://，ss://，ssr://，trojan://， trojan-go:// 等通用分享链接。\r\n\r\n2、服务器配置的JSON数组,支持WinXray格式以及通用格式JSON。\r\n其他 BASE64 编码或明文返回以上1、2条规定的配置或分享链接。')
}
 
winform.btnCopySharedLink.skin(style.link)
winform.btnCopySharedLink.oncommand = function(id,event){
	var lnk = winform.editSharedLink.text;
	if(!#lnk){
		winform.msgErr("分享链接不能为空",1500);
	}
	else {
		win.clip.write(lnk);
		winform.msgOk("分享链接已复制到剪贴板",1500);
	} 	
}


winform.setOutboundConfig = function(cfg){
    winform.cmbProtocol.selText = cfg.protocol;
	winform.cmbProtocol.onListChange();
	
	winform.btnAddOutbound.skin(style.button);
	winform.btnSaveOutbound.skin(style.primaryButton);
	winform.btnSaveOutbound.disabled = false;

	winform.origOutboundConfig = cfg;
	winform.outboundConfig = table.clone(cfg);
	winform.editOutbound.text = web.json.stringify(winform.outboundConfig,true,false);
	onEditChange();
}
winform.setOutboundConfig({
	port = 443;
	protocol = "vmess";
	network = "tcp";
});
winform.btnAddOutbound.skin(style.primaryButton);
winform.btnSaveOutbound.skin(style.button);
winform.btnSaveOutbound.disabled = true;

winform.btnUpdateCore.skin(style.plainButton)
winform.btnUpdateCore.oncommand = function(id,event){ 
	winform.btnUpdateCore.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	var sel = winform.cmbProtocol.selText;
	
	if(sel=="ssr"){
		import xray.core.ssr;
		var versionTag = xray.core.ssr.updateCore();
		if( versionTag ){
			publish("uiCommand.restartxrayCore");
			winform.btnUpdateCore.disabledText = null;
			winform.msgOk("SSR Core（ ShadowsocksR-native ） 已更新到：" + versionTag,1500)
		}
		else {
			winform.btnUpdateCore.disabledText = null;
		} 	
	}
	elseif(sel=="vless"){
		import xray.core.v2ray;
		var versionTag = xray.core.v2ray.updateCore();
		if( versionTag ){
			publish("uiCommand.restartxrayCore");
			winform.btnUpdateCore.disabledText = null;
			winform.msgOk("已下载最新 v2ray Core 版本：" + versionTag+ '\nxray 内核已切换为：v2ray Core',1500)
		}
		else {
			winform.btnUpdateCore.disabledText = null;
			winform.msgErr("v2ray Core 下载失败！");
		} 	
	}
	elseif(sel=="naive"){
		import xray.core.naive;
		var versionTag = xray.core.naive.updateCore();
		if( versionTag ){
			publish("uiCommand.restartxrayCore");
			winform.btnUpdateCore.disabledText = null;
			winform.msgOk("NaÏveProxy Core 已更新到：" + versionTag,1500)
		}
		else {
			winform.btnUpdateCore.disabledText = null;
			winform.msgErr("NaÏveProxy Core 下载失败！");
		} 		
	}	
	else {
		import xray.core;
		var versionTag = xray.core.updateCore();
		if( versionTag ){
			publish("uiCommand.restartxrayCore");
			winform.btnUpdateCore.disabledText = null;
			winform.msgOk("已下载最新 xray Core 版本：" + versionTag + '\nxray 内核已切换为： xray Core',1500)
		}
		else {
			winform.btnUpdateCore.disabledText = null;
			winform.msgErr("xray Core 下载失败！");
		} 
	}
}

winform.qrPicture.skin(
	foreground = { 
		hover = 0x10FFFF00; 
		active = 0x20EEEE00; 
	}
)
winform.qrPicture.oncommand = function(id,event){
    var lnk = string.trim(winform.editSharedLink.text);
    if(#lnk){
    	var frmChild = winform.loadForm("\forms\main\tools\qr.aardio");
		frmChild.createQrCode(lnk);
		frmChild.show();	
    }	
}

winform.show();
win.loopMessage();
return winform;