//rules 路由规则
import xray.core;
namespace xray.core.rules;

var getRulesUrl = function(){
	var msgDlg = ..win.dlg.message(..mainForm); 
	var form = msgDlg.create('正在获取路由规则最新版本',,true)
	form.icon = '\uF1D8';
	form.progress.startProgress(50); 
	
	var url,tag = ..win.invoke(function(){
    	import process;
		import inet.http;
		var http = ..inet.http();
		var url = http.location("https://github.com/Loyalsoldier/v2ray-rules-dat/releases/latest");
		http.close();
		
		if(!url) return;
		
		var tag = ..string.match(url,"[^/]+$")
		if(!tag || (tag=="latest")) return;;

		return "https://github.com/Loyalsoldier/v2ray-rules-dat/releases/download/" 
			+ tag + "/rules.zip",tag	
	});
	form.close();
    
	return url,tag;
} 	


update = function(){
    ..mainForm.disabled = true; 
    	
	var coreDir = ..xray.core.getCoreDir();
	var url,versionTag = getRulesUrl();
	if(!url){		
		..mainForm.disabled = false;
		return null;
	}
	  
	import fsys;
	fsys.delete( ..io.appData("/WinXray/temp/rules/") );
	 
	import zlib.httpFile;
	if( ..zlib.httpFile.download(url,"正在下载最新路由规则，版本：" + versionTag
		,..io.appData("/WinXray/download/")
		,..io.appData("/WinXray/temp/rules/"),,..mainForm.hwnd) ){
			..mainForm.disabled = false;
			
			if(..io.exist(..io.appData("/WinXray/temp/rules/geoip.dat"))){
				
				..io.createDir(coreDir); 
				fsys.copy(..io.appData("/WinXray/temp/rules/*.*"),coreDir);   
				return versionTag;
			}  
	}
	else {
		..mainForm.disabled = false; 
	} 	 
}

check = function(){
	var coreDir = ..xray.core.getCoreDir();
    if(!..io.exist( ..io.joinpath(coreDir,"geoip.dat") ) ) return update();
}